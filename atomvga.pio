/**
 * Copyright (c) 2021 Chris Moulang
 *
 * SPDX-License-Identifier: BSD-3-Clause
*/
.program test

.define public PIN_A0 2
.define PIN_1MHZ 18
.define public PIN_R_NW 19
.define public PIN_SEL1 26

.define NOTHING 0b111
.define ADDRESS_LOW 0b011
.define ADDRESS_HIGH 0b101
.define DATA 0b110

.define tSELECT 3   ; = 20nS at 150MHz
.side_set 3

    set PINDIRS, 0b111      side ADDRESS_LOW            ; set as output
retry:
    mov ISR, x              side ADDRESS_LOW            ; clear ISR
    wait 0 GPIO PIN_1MHZ    side ADDRESS_LOW            ; wait for clock -> 0

.wrap_target
    wait 1 GPIO PIN_1MHZ    side ADDRESS_LOW            ; wait for clock 0 -> 1

    in PINS 8               side ADDRESS_HIGH           ; read address 0-7 
    jmp PIN retry           side ADDRESS_HIGH [tSELECT] ; wait for next clock cycle if not a write
    in PINS 8               side DATA                   ; read address 8-15

    wait 0 GPIO PIN_1MHZ    side DATA                   ; wait for clock 1 -> 0
    in PINS 8               side ADDRESS_LOW            ; read data
.wrap


% c-sdk {
// this is a raw helper function for use by the user which sets up the GPIO output, and configures the SM to output on a particular pin

void test_program_init(PIO pio, uint sm, uint offset) {
   pio_sm_config c = test_program_get_default_config(offset);
   sm_config_set_jmp_pin 	(&c, test_PIN_R_NW);  	
   sm_config_set_in_pins(&c, test_PIN_A0);
   sm_config_set_set_pins(&c, test_PIN_SEL1, 3);

   sm_config_set_sideset(&c, 3, false, false);
   sm_config_set_sideset_pins(&c, test_PIN_SEL1);
   
   sm_config_set_in_shift(&c, true, true, 24);
   pio_sm_init(pio, sm, offset, &c);
}

%}
